#!groovy

def build_and_deploy_docker() {
    def registry = "https://index.docker.io/v1/"
    def credentials_id = "fa515582-07c3-4af2-b4ec-468727e745ba"

    def pull = env.ghprbPullId
    def rev = "--build-arg revision=${sha1}"

    if (pull == null) {
        def image = docker.build("bluebrain/spack", "-f docker/Dockerfile ${rev} .")
        // docker.withRegistry(registry, credentials_id) {
        //     image.push("latest")
        // }
    } else {
        def pr = "--build-arg pr=${env.ghprbPullId}"
        def image = docker.build("bluebrain/spack", "-f docker/Dockerfile ${rev} ${pr} .")
        docker.withRegistry(registry, credentials_id) {
            image.push("test-${env.ghprbPullId}")
        }
    }
}

def deployment_directory(parent) {
    def pull = env.ghprbPullId
    if (parent || pull == null) {
        return "/gpfs/bbp.cscs.ch/ssd/apps/hpc/jenkins"
    } else {
        return "/gpfs/bbp.cscs.ch/ssd/apps/hpc/jenkins/pulls/${pull}"
    }
}

def temporary_directory() {
    def pull = env.ghprbPullId
    if (pull == null) {
        return "${TMPDIR}"
    } else {
        def tmpdir = "/gpfs/bbp.cscs.ch/ssd/apps/hpc/jenkins/pulls/${pull}/tmp"
        dir(tmpdir) {
            writeFile file: "the_bad_touch", text: "created for convenience"
        }
        return tmpdir
    }
}

def post_process() {
    dir("${BASEDIR}") {
        def pull = env.ghprbPullId
        def last = run_stages()[-1]
        def stages = run_stages().join(' ')

        if (params.REDEPLOY) {
            sh "./deploy.sh -l ${stages}"
        }

        sh "./deploy.sh -m ${stages}"
        sh "./deploy.sh -c ${last}"
    }
}

def deployment_stages() {
    return ['compilers',
            'externals',
            'libraries',
            'applications']
}

def deployment_stages_string() {
    return deployment_stages().join('\n')
}

def run_stages() {
    def take = false
    def stages = []
    deployment_stages().each {
        if (it == params.STAGE)
            take = true
        if (take)
            stages << it
    }
    return stages
}

pipeline {
    agent {
        label 'bb5'
    }

    options {
        timestamps()
    }

    parameters {
        booleanParam(name: 'REDEPLOY',
                     defaultValue: false,
                     description: 'Set the "latest" symlink to the currently built deployment')
        string(name: 'DEPLOYMENT_DATE',
               defaultValue: 'latest',
               description: 'Date to use when redeploying, in YYYY-MM-DD format')
        choice(name: 'STAGE',
               choices: deployment_stages_string(),
               description: 'Stage from which to restart the building process')
        booleanParam(name: 'BUILDCACHE',
                     defaultValue: false,
                     description: 'Use cache to install already compiled software')
        booleanParam(name: 'SOURCECACHE',
                     defaultValue: true,
                     description: 'Use cache to install already downloaded sources')
        choice(name: 'BUILD_NODES',
               choices: '1\n2\n3\n4\n5',
               description: 'How many nodes are used building software')
        string(name: 'sha1',
               defaultValue: 'develop',
               description: 'Used for fetching the right Jenkinsfile')
    }

    environment {
        DEPLOYMENT_UPSTREAM = deployment_directory(true)
        DEPLOYMENT_ROOT = deployment_directory(false)
        BASEDIR = "${DEPLOYMENT_ROOT}/spack/deploy"
        DOCKERDIR = "${DEPLOYMENT_ROOT}/spack/docker"
        TMPDIR = temporary_directory()
    }

    // Configured via the UI to allow concurrent PR builds
    // triggers {
    //     cron('H H(0-7) * * *')
    // }

    stages {
        stage('Setup') {
            parallel {
                stage('Production') {
                    when {
                        not {
                            expression {
                                return env.ghprbPullId
                            }
                        }
                    }

                    // General setup: make sure that Spack is installed and that
                    // license files are available to Spack
                    steps {
                        dir("${DEPLOYMENT_ROOT}/spack") {
                            checkout([$class: 'GitSCM',
                                      branches: [[name: "FETCH_HEAD"]],
                                      extensions: [[$class: 'LocalBranch']],
                                      userRemoteConfigs: [[
                                            refspec: "+refs/heads/develop:refs/remotes/origin/develop",
                                            url: "https://github.com/BlueBrain/spack.git"
                                      ]]])
                        }
                        dir("${DEPLOYMENT_ROOT}/spack/etc/spack/licenses") {
                            git(url: "ssh://bbpcode.epfl.ch/hpc/spack-licenses")
                        }
                    }
                }

                stage('PR') {
                    when {
                        expression {
                            return env.ghprbPullId
                        }
                    }

                    // General setup: make sure that Spack is installed and that
                    // license files are available to Spack
                    steps {
                        dir("${DEPLOYMENT_ROOT}/spack") {
                            checkout([$class: 'GitSCM',
                                      branches: [[name: "FETCH_HEAD"]],
                                      extensions: [[$class: 'LocalBranch']],
                                      userRemoteConfigs: [[
                                            refspec: "+refs/pull/${env.ghprbPullId}/head:refs/remotes/origin/pull/${env.ghprbPullId}",
                                            url: "https://github.com/BlueBrain/spack.git"
                                      ]]])
                        }
                        dir("${DEPLOYMENT_ROOT}/spack/etc/spack/licenses") {
                            git(url: "ssh://bbpcode.epfl.ch/hpc/spack-licenses")
                        }
                    }
                }
            }
        }

        // stage('Archival') {
        //     // When tagging a new release, reset the symlinks pointing to
        //     // the last release
        //     when {
        //         tag pattern: "release-\\d+", comparator: "REGEXP"
        //     }

        //     steps {
        //         sh "ls"
        //     }
        // }

        stage('Compilers') {
            when {
                expression {
                    return 'compilers' in run_stages()
                }
            }

            steps {
                dir("${BASEDIR}") {
                    sh "./deploy.sh compilers"
                }
            }

            post {
                always {
                    archiveArtifacts artifacts: "stacks/compilers/*.xml"
                    junit testResults: "stacks/compilers/*.xml", allowEmptyResults: true
                }
            }
        }

        stage('External Software') {
            when {
                expression {
                    return 'externals' in run_stages()
                }
            }

            steps {
                dir("${BASEDIR}") {
                    sh "./deploy.sh externals"
                }
            }

            post {
                always {
                    archiveArtifacts artifacts: "stacks/externals/*.xml"
                    junit testResults: "stacks/externals/*.xml", allowEmptyResults: true
                }
            }
        }



        stage('Libraries') {
            when {
                expression {
                    return 'libraries' in run_stages()
                }
            }

            steps {
                dir("${BASEDIR}") {
                    sh "./deploy.sh libraries"
                }
            }

            post {
                always {
                    archiveArtifacts artifacts: "stacks/libraries/*.xml"
                    junit testResults: "stacks/libraries/*.xml", allowEmptyResults: true
                }
            }
        }

        stage('Applications') {
            when {
                expression {
                    return 'applications' in run_stages()
                }
            }

            steps {
                withEnv(['SOURCECACHE=false',
                         'BUILDCACHE=false']) {
                    dir("${BASEDIR}") {
                        sh "./deploy.sh applications"
                    }
                }
            }

            post {
                always {
                    archiveArtifacts artifacts: "stacks/applications/*.xml"
                    junit testResults: "stacks/applications/*.xml", allowEmptyResults: true
                }

                success {
                    post_process()
                }
            }
        }

        stage('Post-Mortem') {
            parallel {
                stage('Build Documentation') {
                    steps {
                        sh "ls"
                    }
                }

                stage('Delete Old PRs') {
                    when {
                        not {
                            expression {
                                return env.ghprbPullId
                            }
                        }
                    }

                    steps {
                        sh """
                            shopt -s extglob
                            pulls=\$(curl -sG https://api.github.com/repos/BlueBrain/spack/pulls|python -c 'import json,sys;print("|".join(str(p["number"]) for p in json.load(sys.stdin)))')
                            for p in ${DEPLOYMENT_ROOT}/pulls/!(\${pulls}); do
                                rm -rf "\${p}"
                            done
                        """
                    }
                }

                stage("Test Modules") {
                    steps {
                        build job: "hpc.module-testing", parameters: [
                            string(name: "MODULE_SCRIPT_PATH", value: "${DEPLOYMENT_ROOT}/config/modules.sh")
                        ]
                    }
                }

                stage('Build Docker') {
                    agent {
                        label 'docker19'
                    }

                    steps {
                        build_and_deploy_docker()
                    }
                }
            }
        }
    }

    post {
        failure {
            script {
                if (!env.ghprbPullId) {
                    // mail to: 'bbp-deployment@groupes.epfl.ch',
                    //      subject: "Failed Deployment Build: ${currentBuild.fullDisplayName}",
                    //      body: "See ${env.BUILD_URL}"
                }
            }
        }
    }
}
